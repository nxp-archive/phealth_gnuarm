# Copyright (C) 1998, 1999 Cygnus Solutions

load_lib "libgloss.exp"

# GCJ_UNDER_TEST is the compiler under test.

global tmpdir

if ![info exists tmpdir] {
    set tmpdir "/tmp"
}

# Find `jv-scan'.  FIXME: this relies on DejaGnu internals.  These
# should probably be exposed in a better way.
proc find_jvscan {} {
    global tool_root_dir

    set file [lookfor_file $tool_root_dir jv-scan]
    if { $file == "" } {
	set file [lookfor_file $tool_root_dir gcc/jv-scan];
    }
    if {$file == ""} {
	set file jv-scan
    }
    return $file
}

proc bytecompile_file { file objdir {classpath {}} } {
    global env
    set dirname  [file dirname $file];

    catch {unset env(CLASSPATH)}
    if {$classpath != ""} then {
        set env(CLASSPATH) $classpath
    }
    if {[catch {system "cd $dirname; javac $file -d $objdir"} msg]} then {
	verbose "couldn't compile $file: $msg"
	set r 0
    } else {
	set r 1
    }
    return $r
}

set libjava_initialized 0

#
# Build the status wrapper library as needed.
#
proc libjava_init { args } {
    global wrapper_file;
    global wrap_compile_flags;
    global libjava_initialized
    global GCJ_UNDER_TEST
    global TOOL_EXECUTABLE

    if { $libjava_initialized == 1 } { return; }

    if ![info exists GCJ_UNDER_TEST] {
	if [info exists TOOL_EXECUTABLE] {
	    set GCJ_UNDER_TEST $TOOL_EXECUTABLE;
	} else {
	    set GCJ_UNDER_TEST "[find_gcj]"
	}
    }

    set wrapper_file "";
    set wrap_compile_flags "";
    if [target_info exists needs_status_wrapper] {
	set result [build_wrapper "testglue.o"];
	if { $result != "" } {
	    set wrapper_file [lindex $result 0];
	    set wrap_compile_flags [lindex $result 1];
	} else {
	    warning "Status wrapper failed to build."
	}
    }

    set libjava_initialized 1
}

# Compute arguments needed for compiler.
proc libjava_arguments {} {
    global base_dir
    global LIBJAVA
    global LIBGC
    global srcdir subdir objdir
    global TOOL_OPTIONS
    global GCJ_UNDER_TEST
    global tmpdir
    global runtests

    if [info exists LIBJAVA] {
	set libjava $LIBJAVA;
    } else {
	set gp [get_multilibs];
	if { $gp != "" } {
	    if [file exists "$gp/libjava/libgcj.a"] {
		set libjava "-L$gp/libjava -lgcj";
	    }
	}
	if ![info exists libjava] {
	  set libjava [findfile $base_dir/../../libjava/libgcj.a "-L$base_dir/../../libjava -lgcj" -lgcj]
	}
    }

    if [info exists LIBGC] {
	set libgc $LIBGC;
    } else {
	set gp [get_multilibs];
	if { $gp != "" } {
	    if [file exists "$gp/boehm-gc/libgcjgc.a"] {
		set libgc "-L$gp/boehm-gc -lgcjgc";
	    }
	}
	if ![info exists libgc] {
	  set libgc [findfile $base_dir/../../boehm-gc/libgcjgc.a \
		       "-L$base_dir/../../boehm-gc -lgcjgc" \
		       {}]
	}
    }

    if [info exists LIBQTHREADS] {
	set libqthreads $LIBQTHREADS
    } else {
	set gp [get_multilibs]
	if { $gp != "" } {
	    if [file exists "$gp/qthreads/libgcjcoop.a"] {
		set libqthreads "-L$gp/qthreads -lgcjcoop";
	    }
	}
	if ![info exists libqthreads] {
	    set libqthreads [findfile $base_dir/../../qthreads/libgcjcoop.a \
			       "-L$base_dir/../../qthreads -lgcjcoop" \
			       {}]
	}
    }

    # FIXME: there's no way to determine whether -lpthread is
    # required.  We should get this info from configure, or it should
    # just be in the compiler driver.

    verbose "using LIBJAVA = $libjava" 2
    verbose "using LIBGC = $libgc" 2
    verbose "using LIBQTHREADS = $libqthreads" 2
    set args ""

    # Basically we want to build up a colon separated path list from
    # the value of $libjava.

    # First strip away any -L arguments.
    regsub -all -- "-L" $libjava "" ld_library_path

    # Then remove any -lgcj argument.
    regsub -all -- " -lgcj.*" $ld_library_path "" ld_library_path

    # First strip away any -L arguments.
    regsub -all -- "-L" $libgc $ld_library_path ld_library_path

    # Then remove any -lgcjgc argument.
    regsub -all -- " -lgcjgc.*" $ld_library_path "" ld_library_path

    # That's enough to make things work for the normal case.
    # If we wanted to handle an arbitrary value of libjava,
    # then we'd have to do a lot more work.

    # Set variables the dynamic linker looks at.
    setenv LD_LIBRARY_PATH $ld_library_path
    setenv SHLIB_PATH $ld_library_path

    # Set the CLASSPATH environment variable
    verbose "CLASSPATH is $objdir/.."
    global env
    set env(CLASSPATH) "$objdir/.."

    global wrapper_file wrap_compile_flags;
    lappend args "additional_flags=$wrap_compile_flags";
    lappend args "libs=$wrapper_file";
    lappend args "libs=$libjava";
    lappend args "libs=$libgc";
    lappend args "libs=$libqthreads"
    lappend args debug

    if { [target_info needs_status_wrapper]!="" && [info exists gluefile] } {
	lappend args "libs=${gluefile}"
	lappend args "ldflags=$wrap_flags"
    }

    if [info exists TOOL_OPTIONS] {
	lappend args "additional_flags=$TOOL_OPTIONS"
    }
    lappend args "compiler=$GCJ_UNDER_TEST"

    return $args
}


#
# Run the test specified by srcfile and resultfile. compile_args and
# exec_args are additional arguments to be passed in when compiling and
# running the testcase, respectively.
#
proc test_libjava_from_source { options srcfile compile_args inpfile resultfile exec_args } {
    global base_dir
    global LIBJAVA
    global LIBGC
    global srcdir subdir objdir
    global TOOL_OPTIONS
    global GCJ_UNDER_TEST
    global tmpdir
    global runtests

    set errname [file rootname [file tail $srcfile]]
    if {! [runtest_file_p $runtests $errname]} {
        return
    }

    set args [libjava_arguments]
    # Add the --main flag
    lappend args "additional_flags=--main=[file rootname [file tail $srcfile]]"
    if { $compile_args != "" } {
	lappend args "additional_flags=$compile_args"
    }

    regsub "^.*/(\[^/.\]+)\[.\]\[^/]*$" "$srcfile" "\\1" out
    set executable "${objdir}/$out"
    if { $compile_args != "" } {
	set errname "$errname $compile_args"
    }

    if { [target_compile $srcfile "$executable" executable $args] != "" } {
	fail "$errname compilation from source"
	setup_xfail "*-*-*"
	fail "$errname execution from source compiled test"
	setup_xfail "*-*-*"
	fail "$errname output from source compiled test"
	return;
    }
    pass "$errname compilation from source"

    if { $exec_args == "no-exec" } {
	return
    }

    set result [libjava_load $executable "$exec_args" "$inpfile"];
    set status [lindex $result 0];
    set output [lindex $result 1];
    $status "$errname execution from source compiled test"
    if { $status != "pass" } {
	setup_xfail "*-*-*"
	fail "$errname output from source compiled test"
	return;
    }

    verbose "resultfile is $resultfile"
    set id [open $resultfile r];
    set expected ""
    append expected [read $id];
    regsub -all "\r" "$output" "" output;
    regsub "\n*$" $expected "" expected
    regsub "\n*$" $output "" output
    regsub "^\n*" $expected "" expected
    regsub "^\n*" $output "" output
    regsub -all "\[ \t\]\[ \t\]*" $expected " " expected
    regsub -all "\[ \t\]*\n\n*" $expected "\n" expected
    regsub -all "\[ \t\]\[ \t\]*" $output " " output
    regsub -all "\[ \t\]*\n\n*" $output "\n" output
    verbose "expected is $expected"
    verbose "actual is $output"
    set passed 0;
    if {$options == "regexp_match"} {
	if [regexp $expected $output] {
	    set passed 1;
	}
    } else {
	if { $expected == $output } {
	    set passed 1;
	}
    }
    if { $passed == 1 } {
	pass "$errname output from source compiled test"
    } else {
	clone_output "expected was $expected"
	clone_output "output was $output"
	fail "$errname output from source compiled test"
    }
    close $id;
}

#
# Run the test specified by srcfile and resultfile. compile_args and
# exec_args are additional arguments to be passed in when compiling and
# running the testcase, respectively.
#
proc test_libjava_from_javac { options srcfile compile_args inpfile resultfile exec_args } {
    global base_dir
    global LIBJAVA
    global LIBGC
    global srcdir subdir objdir
    global TOOL_OPTIONS
    global GCJ_UNDER_TEST
    global tmpdir
    global runtests

    set errname [file rootname [file tail $srcfile]]
    if {! [runtest_file_p $runtests $errname]} {
        return
    }

    # bytecompile files with Sun's compiler for now.
    set bc_ok [bytecompile_file $srcfile $objdir]
    if {! $bc_ok} then {
	fail "$errname byte compilation"
	setup_xfail "*-*-*"
	fail "$errname compilation from bytecode"
	setup_xfail "*-*-*"
	fail "$errname execution from bytecode->native test"
	setup_xfail "*-*-*"
	fail "$errname output from bytecode->native test"
	return
    }
    pass "$errname byte compilation"

    # Find name to use for --main, and name of all class files.
    set jvscan [find_jvscan]
    verbose "jvscan is $jvscan"
    set main_name [string trim \
		     [target_compile $srcfile "" none \
			"compiler=$jvscan additional_flags=--print-main"]]
    verbose "main name is $main_name"
    set class_out [string trim \
		     [target_compile $srcfile "" none \
			"compiler=$jvscan additional_flags=--list-class"]]
    verbose "class list is $class_out"
    # FIXME: As of Wed Feb 24 1999, `jv-scan --list-class' prints
    # nothing if the file contains an interface and not a class.  I
    # believe this is a jv-scan bug.
    if {$class_out == ""} then {
	set class_files [file rootname [file tail $srcfile]].class
    } else {
	# Turn "a b" into "a.class b.class".
	set class_files [join [split [string trim $class_out]] ".class "].class
    }

    # Initial arguments.
    set args [libjava_arguments]

    # Usually it is an error for a test program not to have a `main'
    # method.  However, for no-exec tests it is ok.
    if {$main_name == ""} {
	if {$exec_args != "no-exec"} {
	    perror "No `main' given in program $errname"
	    return
	} else {
	    set type object
	    set executable [file rootname [file tail $srcfile]].o
	}
    } else {
	set type executable
	lappend args "additional_flags=--main=$main_name"
	set executable "${objdir}/$main_name"
    }

    if { $compile_args != "" } {
	lappend args "additional_flags=$compile_args"
    }

    if { $compile_args != "" } {
	set errname "$errname $compile_args"
    }

    if { [target_compile $class_files "$executable" $type $args] != "" } {
	fail "$errname compilation from bytecode"
	setup_xfail "*-*-*"
	if {$exec_args != "no-exec"} {
	    fail "$errname execution from bytecode->native test"
	    setup_xfail "*-*-*"
	    fail "$errname output from bytecode->native test"
	}
	return;
    }
    pass "$errname compilation from bytecode"

    if { $exec_args == "no-exec" } {
	return
    }

    set result [libjava_load $executable "$exec_args" "$inpfile"];
    set status [lindex $result 0];
    set output [lindex $result 1];
    $status "$errname execution from bytecode->native test"
    if { $status != "pass" } {
	setup_xfail "*-*-*"
	fail "$errname output from bytecode->native test"
	return;
    }

    verbose "resultfile is $resultfile"
    set id [open $resultfile r];
    set expected ""
    append expected [read $id];
    regsub -all "\r" "$output" "" output;
    regsub "\n*$" $expected "" expected
    regsub "\n*$" $output "" output
    regsub "^\n*" $expected "" expected
    regsub "^\n*" $output "" output
    regsub -all "\[ \t\]\[ \t\]*" $expected " " expected
    regsub -all "\[ \t\]*\n\n*" $expected "\n" expected
    regsub -all "\[ \t\]\[ \t\]*" $output " " output
    regsub -all "\[ \t\]*\n\n*" $output "\n" output
    verbose "expected is $expected"
    verbose "actual is $output"
    set passed 0;
    if {$options == "regexp_match"} {
	if [regexp $expected $output] {
	    set passed 1;
	}
    } else {
	if { $expected == $output } {
	    set passed 1;
	}
    }
    if { $passed == 1 } {
	pass "$errname output from bytecode->native test"
    } else {
	clone_output "expected was $expected"
	clone_output "output was $output"
	fail "$errname output from bytecode->native test"
    }
    close $id;
}

#
# Run the test specified by srcfile and resultfile. compile_args and
# exec_args are additional arguments to be passed in when compiling and
# running the testcase, respectively.
#
proc test_libjava { options srcfile compile_args inpfile resultfile exec_args } {
    test_libjava_from_source $options $srcfile $compile_args $inpfile $resultfile $exec_args
    test_libjava_from_javac $options $srcfile $compile_args $inpfile $resultfile $exec_args
 }

#
# libjava_version -- extract and print the version number of libjavap
#
proc default_libjava_version {} {
}

proc default_libjava_start { } {
}
